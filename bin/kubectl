#!/bin/zsh

:main() {
    local arg
    local context
    local args=()
    local entity
    local namespace
    local all_namespaces

    for (( i = 1; i <= $#; i ++ )); do
        arg=${@:$i:1}

        if [[ "$arg" =~ ^--context ]]; then
            context=${arg:9}

            if [[ ! "$context" ]]; then
                (( i++ ))
                context=${@:$i:1}
            fi

            continue
        fi

        if [[ "$arg" =~ ^-n ]]; then
            namespace=${arg:2}

            if [[ ! "$namespace" ]]; then
                (( i++ ))
                namespace=${@:$i:1}
            fi

            continue
        fi

        if [[ "$arg" =~ ^--namespace ]]; then
            namespace=${arg:12}

            if [[ ! "$namespace" ]]; then
                (( i++ ))
                namespace=${@:$i:1}
            fi

            continue
        fi

        if [[ "$arg" =~ \\+.* ]]; then
            if [[ "${arg:1}" == "!" ]]; then
                all_namespaces=true
            else
                namespace=$(
                    :kubectl get namespaces --no-headers -o name \
                        | grep -F "${arg:1}" \
                        | cut -f2 -d/
                )
            fi

            continue
        fi

        if [[ ! "$context" && "$arg" =~ ^@.* ]]; then
            context=$(
                :kubectl config get-contexts --no-headers -o name \
                    | grep -F "${arg:1}"
            )

            continue
        fi

        args+=("$arg")
    done

    if [[ ! "$context" ]]; then
        context=$(:kubectl config current-context)

        if [[ ! "$context" ]]; then
            printf ":: no matching context found\n"
            return 1
        fi

        printf ":: @ %s\n" "$context"
    fi

    if [[ "$namespace" ]]; then
        printf ":: + %s\n" "$namespace"
    fi

    if [[ "$all_namespaces" ]]; then
        printf ":: + all namespaces\n"
    fi

    set -- "${args[@]}"

    args=()
    targets=()

    for arg in "$@"; do
        if [[ "$arg" =~ %.* ]]; then
            entity=${arg%/*}

            if [[ "$entity" == "$arg" ]]; then
                entity=pod
            fi

            entities=$(
                :kubectl \
                    ${namespace:+--namespace=$namespace} \
                    --context=$context get "$entity" --no-headers \
                        | awk '$3 != "Terminating" { print $1 }' \
                        | cut -f2- -d/ \
                        | grep "^${${arg:1}#*/}" \
                        | paste -sd ' '
            )

            entities=(${(s/ /)entities})

            if [[ ! "$entities" ]]; then
                printf ":: no matching entities found for '%s'\n" "$arg"
                return 1
            else
                for entity in "${entities[@]}"; do
                    printf ":: %% %s\n" "${entity}"
                done
            fi

            args+=($entities)
        else
            args+=("$arg")
        fi
    done

    :kubectl --context=$context \
        ${all_namespaces:+--all-namespaces} \
        ${namespace:+--namespace=$namespace} \
        "${args[@]}"
}

:kubectl() {
    /usr/bin/kubectl "$@"
}

:main "$@"
